---
title: Spock random order of tests - how to?
date: 2019-04-06 10:52:11
updated: 2019-04-06 10:52:11
tags:
    - groovy
    - spock
    - gradle
    - unit-test
    - random
categories:
    - Groovy Cookbook
cover: /images/spock-random-tests-bg.jpg
og_image: /images/og/spock-random-tests.jpg
eyeCatchImage: /images/og/spock-random-tests.jpg
---

http://spockframework.org/[Spock Framework] executes test methods _(features)_ in a single class _(specification)_ in the declaration order.
There is nothing wrong in this default behavior - we should write tests with their isolation in mind.
However, in some cases, we would like to randomize test methods execution.
Today we are going to learn how to do it.

++++
<!-- more -->
++++

== Introduction

Let's start with a reasonably simple specification that prints a number to the console.

.src/test/groovy/com/github/wololock/RandomSpockSpec.groovy
[source,groovy]
----
package com.github.wololock

import spock.lang.Specification

class RandomSpockSpec extends Specification {

    def "test 1"() {
        when:
        def number = 1

        then:
        println "[${new Date().format("HH:mm:ss.SSS")}] number ${number}"
    }

    def "test 2"() {
        when:
        def number = 2

        then:
        println "[${new Date().format("HH:mm:ss.SSS")}] number ${number}"
    }

    def "test 3"() {
        when:
        def number = 3

        then:
        println "[${new Date().format("HH:mm:ss.SSS")}] number ${number}"
    }

    def "test 4"() {
        when:
        def number = 4

        then:
        println "[${new Date().format("HH:mm:ss.SSS")}] number ${number}"
    }

    def "test 5"() {
        when:
        def number = 5

        then:
        println "[${new Date().format("HH:mm:ss.SSS")}] number ${number}"
    }
}
----

When we execute this specification, we get all numbers printed in the ascending order.

.The output of test's execution
[source,plain]
----
[10:54:13.469] number 1
[10:54:13.512] number 2
[10:54:13.512] number 3
[10:54:13.513] number 4
[10:54:13.513] number 5
----

NOTE: You can find the source code of this example in the https://github.com/wololock/spock-random-order-demo[following repository].

== Forcing random order

There is no simple switch to change Spock's default behavior of ordering features execution. However, we can
extend the default runner class - `Sputnik`. We are going to override the `run(RunNotifier notifier)` method so
that we can replace the default features execution number of each feature with the random number.
Here is our extended `Sputnik` class called `RandomOrder`:

.src/test/groovy/com/github/wololock/RandomOrder.groovy
[source,groovy]
----
package com.github.wololock

import org.junit.runner.notification.RunNotifier
import org.junit.runners.model.InitializationError
import org.spockframework.runtime.RunContext
import org.spockframework.runtime.Sputnik
import org.spockframework.runtime.model.SpecInfo

class RandomOrder extends Sputnik {
    RandomOrder(Class clazz) throws InitializationError {
        super(clazz)
    }

    @Override
    void run(RunNotifier notifier) {
        super.runExtensionsIfNecessary()
        super.generateSpecDescriptionIfNecessary()

        final SpecInfo spec = super.getSpec()
        final List<Integer> order = (0..(spec.features.size())) as ArrayList

        Collections.shuffle(order)

        spec.features.each { feature ->
            feature.executionOrder = order.pop()
        }

        RunContext.get().createSpecRunner(spec, notifier).run()
    }
}
----

The only thing that left to do is to add `@RunWith(RandomOrder)` annotation over our specification class.

[source,groovy]
----
package com.github.wololock

import org.junit.runner.RunWith
import spock.lang.Specification

@RunWith(RandomOrder)
class RandomSpockSpec extends Specification {

    def "test 1"() {
        when:
        def number = 1

        then:
        println "[${new Date().format("HH:mm:ss.SSS")}] number ${number}"
    }

    // same body as in the previous example
}
----

Let's execute the test and see the result. In this example, we use Gradle to execute the test.

[source,bash]
----
$ ./gradlew --no-daemon -Dtest.single=RandomSpockSpec test --rerun-tasks
----

Here is the test result report in the HTML format.

[.text-center]
--
[.img-responsive.img-thumbnail]
[link=/images/spock-random-tests.jpg]
image::/images/spock-random-tests.jpg[]
--

== The downsides

If you decide to randomize your Spock features execution with the following approach, you need to be aware of one
significant downside. You need to keep in mind that in this example we have overridden the `Sputnik.run()` method.
Any time you upgrade Spock Framework version, you will have to check if the new version of Spock did not modify
this method's implementation, and if so, you need to reflect those changes on your side as well. However,
Spock upgrades are not released often, so it shouldn't be a huge problem. If the random execution order is
something you desire - go for it!

== IntelliJ IDEA display order

There is one thing worth mentioning regarding IntelliJ IDEA. Even though it executes features in the random order,
it displays the list of methods in the declaration order. It also collects the output from every single feature
and displays the final output in the same order as the list of methods. Take a look at the following screenshot:

[.text-center]
--
[.img-responsive.img-thumbnail]
[link=/images/spock-random-idea.jpg]
image::/images/spock-random-idea.jpg[]
--

We can verify that the order was not alphabetical based on the timestamps added at the beginning of every console log line.

You can also watch the following video where I show Spock's features random order in action.

++++
<div class="row"><div class="col-md-10 col-md-offset-1"><div class="video-container">
<iframe width="560" height="315" src="https://www.youtube.com/embed/Y8cuESttcG4" frameborder="0" allow="accelerometer; autoplay; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
</div></div></div>
++++


